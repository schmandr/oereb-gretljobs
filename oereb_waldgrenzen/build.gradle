apply plugin: "de.undercouch.download"
apply plugin: "ch.so.agi.gretl"

import ch.so.agi.gretl.api.TransferSet
import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download

def pathToTempFolder = System.getProperty("java.io.tmpdir")

def iliModelBasis = "OeREBKRM_V2_0"
def iliModelTransferstruktur = "OeREBKRMtrsfr_V2_0"
def iliModelSymbole = "SO_AGI_OeREB_Legendeneintraege_20211020"

def dbSchemaStaticForestPerimeterOereb = "awjf_statische_waldgrenzen_oereb"
    
def staticForestPerimeterDataSet = "ch.so.awjf.oereb_statische_waldgrenzen"
def symbolsDataSet = staticForestPerimeterDataSet + ".symbole" 

def symbolsFileName = symbolsDataSet + ".xtf"    
def xtfFileName = staticForestPerimeterDataSet + ".xtf"
def zipFileName = staticForestPerimeterDataSet + "_V2_0_xtf.zip"
def emptyTransferFile = "ch.so.awjf.oereb_statische_waldgrenzen.empty.xtf"

def responsibleOfficesBaseUrl = "https://s3.eu-central-1.amazonaws.com/ch.so.agi.geodata-dev/"
def responsibleOfficesDataSet = "ch.so.agi.oereb_zustaendigestellen_V2_0"

def bucketSuffix = '-undefined'
if ( gretlEnvironment == 'production' ) {
    bucketSuffix = ''
} else if ( gretlEnvironment == 'integration' || gretlEnvironment == 'dev' ) {
    bucketSuffix = '-' + gretlEnvironment.substring(0, 3)
} else {
    bucketSuffix = '-' + gretlEnvironment
}
def s3AgiTargetBucket = "ch.so.agi.geodata$bucketSuffix"

task deleteFromOereb(type: SqlExecutor) {
    description = "Löscht die Daten aus dem Transferschema."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = ["delete_oereb_static_forestperimeter_tables.sql"]
}

task downloadResponsibleOffices(type: Download, dependsOn: 'deleteFromOereb') {
    doFirst {
        println responsibleOfficesBaseUrl + responsibleOfficesDataSet + ".xtf"
    }
    description = "Download zuständige Stellen ($responsibleOfficesDataSet)."
    src responsibleOfficesBaseUrl + responsibleOfficesDataSet + ".xtf"
    dest pathToTempFolder
    overwrite true
}

task importResponsibleOfficesToOereb(type: Ili2pgReplace, dependsOn: 'downloadResponsibleOffices') {
    description = "Import zuständige Stellen ($responsibleOfficesDataSet)."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    models = iliModelBasis
    dbschema = dbSchemaStaticForestPerimeterOereb
    dataFile = file(Paths.get(pathToTempFolder.toString(), responsibleOfficesDataSet + ".xtf"))
    importBid = true
    dataset = responsibleOfficesDataSet
}

task importSymbolsToOereb(type: Ili2pgReplace, dependsOn: 'deleteFromOereb') {
    description = "Import Symbole ($symbolsDataSet)."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    models = iliModelSymbole
    dbschema = dbSchemaStaticForestPerimeterOereb
    dataFile = file(symbolsFileName)
    importBid = true
    dataset = symbolsDataSet
}

task importEmptyTransferToOereb(type: Ili2pgReplace, dependsOn: 'deleteFromOereb') {
    description = "Import eines leeren Waldgrenzen-Basket damit notwendige Datasets- und Basketrecords für Transferdaten in der DB erstellt werden."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    models = iliModelTransferstruktur
    dbschema = dbSchemaStaticForestPerimeterOereb
    dataFile = file(emptyTransferFile)
    importBid = true
    dataset = staticForestPerimeterDataSet
}

task transferData(type: SqlExecutor, dependsOn: ["deleteFromOereb", "importResponsibleOfficesToOereb", "importEmptyTransferToOereb"]) {
    description = "Führt den Datenumbau in das Transferschema durch."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    sqlFiles = ["insert_oereb_static_forestperimeter_tables.sql"]
}

task exportData(type: Ili2pgExport, dependsOn: "transferData") {
    description = "Exportiert die umgebauten Daten aus dem Transferschema in ein INTERLIS-Datei."
    database = [dbUriEdit, dbUserEdit, dbPwdEdit]
    models = iliModelTransferstruktur
    dbschema = dbSchemaStaticForestPerimeterOereb
    dataFile = file(Paths.get("$rootDir", xtfFileName))
    dataset = staticForestPerimeterDataSet
    disableValidation = true
}

task replaceWmsServer(dependsOn: "exportData") {
    description = "Ersetzt verschiedene Elemente (Host, Pfad, etc.) der WMS-Requests."
    doLast {
        ant.replace(
        file: file(Paths.get("$rootDir", xtfFileName)),
        token: '${wmsHost}',
        value: 'https://' + geoservicesHostName,
        encoding: 'UTF-8')
    }
}

task validateData(type: IliValidator, dependsOn: "replaceWmsServer") {
    description = "Validiert die exportierten Daten in der Transferstruktur inkl. der externen Beziehungen."
    dataFiles = [
                 file(Paths.get(pathToTempFolder.toString(), responsibleOfficesDataSet + ".xtf")), 
                 file(Paths.get("$rootDir", xtfFileName))
                ]
    logFile = "ilivalidator.log"
    allObjectsAccessible = true
}

task importDataToStage(type: Ili2pgReplace, dependsOn: "validateData") {
    description = "Import des Waldgrenzen-ÖREB-Datensatz in das Stage-Schema."
    database = [dbUriOereb, dbUserOereb, dbPwdOereb]
    models = iliModelTransferstruktur
    dbschema = "stage"
    dataFile = file(Paths.get("$rootDir", xtfFileName))
    dataset = staticForestPerimeterDataSet 
    importBid = true
    disableValidation = true
}

// task refreshOerebWMSTablesStage(type: SqlExecutor) {
//     description = "Aktualisiert OEREB WMS Tabellen."
//     database = [dbUriOereb, dbUserOereb, dbPwdOereb]
//     sqlFiles = ["update_oerebwms_staticforestperimeters_tables.sql"]
//     sqlParameters = [dbSchema: 'stage']
// }

task importDataToLive(type: Ili2pgReplace) {
    description = "Import des Waldgrenzen-ÖREB-Datensatz in das Live-Schema."
    database = [dbUriOereb, dbUserOereb, dbPwdOereb]
    models = iliModelTransferstruktur
    dbschema = "live"
    dataFile = file(Paths.get("$rootDir", xtfFileName))
    dataset = staticForestPerimeterDataSet 
    importBid = true
    disableValidation = true
}

// task refreshOerebWMSTablesLive(type: SqlExecutor) {
//     description = "Aktualisiert OEREB WMS Tabellen."
//     database = [dbUriOereb, dbUserOereb, dbPwdOereb]
//     sqlFiles = ["update_oerebwms_staticforestperimeters_tables.sql"]
//     sqlParameters = [dbSchema: 'live']
// }

task zipXtfFile(type: Zip) {
    description = "Zipt das XTF-File mit den Waldgrenzen für den Upload nach S3"
    from "$rootDir"
    include xtfFileName
    archiveName zipFileName
    destinationDir(file("$rootDir"))
}

task uploadXtfToS3Geodata(type: S3Upload, dependsOn: "zipXtfFile") {
    description = "XTF-File nach S3 hochladen."
    accessKey = awsAccessKeyAgi
    secretKey = awsSecretAccessKeyAgi
    sourceFile = file(Paths.get("$rootDir", zipFileName))
    endPoint = "https://s3.eu-central-1.amazonaws.com"
    region = "eu-central-1"
    bucketName = s3AgiTargetBucket
    acl = "public-read"
}